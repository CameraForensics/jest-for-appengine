group 'com.cameraforensics'
version '2.0.1-5.5.0-5-1.0'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.+"
    }
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

def appengineVersion = "1.9.49"

jar {
    manifest {
        attributes 'Implementation-Title': 'jest-for-appengine',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.searchbox:jest:2.0.1'
    compile 'org.apache.lucene:lucene-core:5.5.0'
    compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifactoryPublish {
    dependsOn sourcesJar, javadocJar
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations {
    published
}

artifactory {
    contextUrl = "${artifactoryUrl}"
    publish {
        repository {
            repoKey = "libs-release-local"
            username = "${artifactoryUsername}"
            password = "${artifactoryPassword}"
        }
        defaults {
            publishConfigs('archives', 'published')
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
        }
    }
}